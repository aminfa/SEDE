grammar de.upb.sede.dsl.Seco with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

//generate seco "http://www.upb.de/sede/dsl/Seco"

import "platform:/resource/de.upb.sede.dsl/model/current/Seco.ecore"

Entries:
	(
    	entities+=EntityClassDefinition
    )* 
    (
    	instructions+=Assignment |
    	instructions+=Yield
    )* ;
    
/*
 * E.g.:
 *  a = fruits.Apple::__construct({i1=0,i2=0,i3=10,i4=10})
 */
 

Operation:
	(=> contextField = Field | entityName = EntityName) ("::")
		 method=MethodName "(" "{"? 
				(
					(args+=Argument)		// First argument
					(',' args+=Argument)*  // Additional Arguments
				)? "}" ? ")" 
;
								


Argument:
	(((indexed ?= "i" index = INT)| parameterName = ID) '=')? value = FieldValue
;


Assignment:
	(=> assignedFields += Field (multiAssignment ?= ',' assignedFields+= Field)* "=")? value = FieldValue ";"
;


/*
 * E.g.:
 * class : a.b.C  wraps d.e.F extends g.H, i.J {
 * 		static method:  Construct (Number, String, some.Entity) -> (Bool, some.other.Entity) {
 * 			"runtime-data" = {
 * 				"some_data_java_needs" = 10
 * 			}
 * 		};
 * 		cast: <-> $some.Other.Entity;
 * }
 */
EntityClassDefinition:
	("class" ":") qualifiedName = EntityName  
	(
		(wrapper ?= "wraps" 
			wrappedEntity = EntityName
		)? &
		(extension ?= "extends" 
			baseEntities += EntityName 
			(',' baseEntities += EntityName)*
		)?
	)
	"{"
		(
			methods += EntityMethod |
			casts += EntityCast
		)* 
	"}"
;


EntityMethod:
	(property = EntityMethodProp)? "method" ":" methodName = MethodName
	 paramSignature = EntityMethodParamSignature 
	 (realization ?= ('realized' | 'realised') 'by' 
	 	methodRealization = MethodName
	 	// realizationArguments EntityMethodParamSignature // TODO parameter redirects
	 )?
	 (additionalData = Json)? ';'
;

enum EntityMethodProp:
	MUTATING = 'mutating' | STATIC = 'static' | PURE = 'pure'
;


EntityMethodParamSignature:
	{EntityMethodParamSignature} 
	("(" 
	 	(parameters += EntityMethodParam ("," parameters += EntityMethodParam)*)?
	 ")")
	 ('->' '(' 
	 	(outputs += EntityMethodParam ("," outputs += EntityMethodParam)*)? 
	 ')')?
;


EntityMethodParam:
	(
		(final ?= "final")?
	)  
	parameterType = EntityName
	  (
	  	parameterName = ID
	  )?
	  (
	  	valueFixed ?= "=" fixedValue = FieldValue
	  )?
;


EntityCast:
	"cast" ":" direction = TransformDirection resultingEntity = EntityName (additionalData = Json)? ';' 
;

enum TransformDirection:
	BI = '<->' | BI = 'to and from' | 
	TO = '->' | TO = 'to' |
	FROM = '<-' | FROM = 'from'
;

Yield:
	=> "yield" yields += FieldValue (multiYield ?= ',' yields += FieldValue)* ';'?
;



QualifiedName  returns ecore::EString:
	-> ID ('.' ID)*
;
	
EntityName returns ecore::EString:
     '$'? QualifiedName ;
     
MethodName returns ecore::EString:
	 '!'? ID ;
    
StringConst returns ecore::EString:
	 STRING;
	
BoolConst returns ecore::EString:
	 "True" | "true" | "False" | "false" | "TRUE" | "FALSE";
	
NullConst returns ecore::EString:
	 "Null" | "null" | "NULL" | "NuLL";
	
NumberConst returns ecore::EString:
	 ("-" | "+")? INT ("." INT)? (("e"|"E") ("-" | "+") INT)?;
	 
	 
Field:
	name = ID (-> dereference ?= '->' memeber = __TerminalField)?;
	
__TerminalField returns FieldValue:
	FieldValue
;
	
FieldValue:
	( -> cast ?='(' castTarget = EntityName ')'  castValue = __TerminalField) |
	number = NumberConst |
	string = StringConst | 
	bool = BoolConst | 
	null ?= NullConst | 
//	(entityReference ?= "ref"  entity = EntityName ('::' method=MethodName)?) | // TODO: do we need entity references?
	(=> operation = Operation) | 
	(field = Field)
;


Json returns ecore::EString:
	'{' 
		((JsonObjectMemeber)
		(',' JsonObjectMemeber)*)? 
	'}';

JsonObjectMemeber returns ecore::EString:
	STRING ':' JsonObjectValue;
	
	
JsonObjectValue returns ecore::EString:
	Json | StringConst | JsonArray |  'true' | 'false' | 'null' | NumberConst;

JsonArray returns ecore::EString:
	'[' ((JsonObjectValue) (',' JsonObjectValue)*)? ']';

