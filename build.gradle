subprojects {
    /*
        configuration for all sub projects.
     */
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    group = 'de.upb.sede'
    version = '1.0'

    repositories {
        jcenter()
        mavenCentral()

        // we use jitpack in order to add dependecy to github projects:
        maven { url 'https://jitpack.io' }

        // libraries we only have jar's from:
        flatDir {
            dirs "${project.rootDir}/libs"
        }
    }
    dependencies {
        testCompile "junit:junit:4.12"
        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'
        compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.0'
    }

    /* mark testrec as resources to be loaded during tests */
    sourceSets {
        test {
            resources {
                srcDir "${project.rootDir}/testrsc"
            }
        }
    }

    /* Set working directory of tests to root folder: */
    test{
        workingDir = new File("${project.rootDir}")
    }
}
/*
    configurations for each sub project.
 */
project(':SEDE.core') {
    dependencies {
        // https://mvnrepository.com/artifact/com.googlecode.json-simple/json-simple
        compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    }
}


project(':SEDE.deployment') {
    dependencies {
        compile project(':SEDE.core')
        compile project(':SEDE.http-apps')
        // https://mvnrepository.com/artifact/commons-cli/commons-cli
        compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
        // https://mvnrepository.com/artifact/commons-io/commons-io
        compile group: 'commons-io', name: 'commons-io', version: '2.6'
    }
}

project(':SEDE.gateway') {
    dependencies {
        compile project(':SEDE.core')

        // https://mvnrepository.com/artifact/org.skyscreamer/jsonassert
        testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'

        implementation 'com.github.aminfa:TinyJavaDot:v1.3'
    }
}

project(':SEDE.executor') {
    dependencies {
        testCompile project(':SEDE.services:demolib')
        compile project(':SEDE.core')
        compile project(':SEDE.gateway')
    }
}

project(':SEDE.http-apps') {
    dependencies {
        testCompile project(':SEDE.services:demolib')
        testCompile project(':SEDE.services:imaging')
        compile project(':SEDE.core')
        compile project(':SEDE.gateway')
        compile project(':SEDE.executor')
    }
}